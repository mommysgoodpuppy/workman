@raw;
-- Workman Raylib Test
from "raylib.h" import {
  "InitWindow" as initWindow,
  "CloseWindow" as closeWindow,
  "WindowShouldClose" as windowShouldClose,
  "SetTargetFPS" as setTargetFPS,
  "BeginDrawing" as beginDrawing,
  "EndDrawing" as endDrawing,
  "ClearBackground" as clearBackground,
  "DrawCircleV" as drawCircleV,
  "DrawText" as drawText,
  "DrawFPS" as drawFPS,
  Color,
  Vector2
};

from "std/zig/types" import {U8, F32};


let main = () => {
  let screenWidth = 800;
  let screenHeight = 450;
  let ballRadius:F32 = 20;

  initWindow(screenWidth, screenHeight, "Workman Raylib Test");
  setTargetFPS(60);

  let darkBg = .{ r = 30, g = 30, b = 40, a = 255 };
  let ballColor = .{ r = 255, g = 0, b = 0, a = 255 };
  let textColor = .{ r = 200, g = 200, b = 200, a = 255 };

  let rec gameLoop = (posX, posY, velX, velY) => {
    if (windowShouldClose()) {
      () 
    } else {

      let newX = posX + velX;
      let newY = posY + velY;

      let newVelX = if (newX >= screenWidth - ballRadius || newX <= ballRadius) {
        velX * -1 
      } else { 
        velX 
      };

      let newVelY = if (newY >= screenHeight - ballRadius || newY <= ballRadius) {
        velY * -1 
      } else { 
        velY 
      };

      beginDrawing();
      clearBackground(darkBg);
      drawCircleV(.{ x = newX, y = newY }, ballRadius, ballColor);
      drawText("Bouncing Ball - Press ESC to exit", 10, 10, 20, textColor);
      drawFPS(screenWidth - 100, 10);
      endDrawing();

      gameLoop(newX, newY, newVelX, newVelY)
    }
  };

  gameLoop(screenWidth / 2, screenHeight / 2, 5, 4);
  closeWindow();
};
