-- Parser smoke tests for Workman parser prototype

from "std/coretypes" import { Empty, Link };
from "std/coretypes" import { IOk, IErr };
from "std/nativeString" import { stringConcat, stringEq, intToString };
from "./parser.wm" import { parseProgram };
from "./ast.wm" import {
  Expr,
  ExprKind,
  IdentifierExpr,
  NumberExpr,
  StringExpr,
  CallExpr,
  ParseError,
  exprKind
};

record frame = {
  pixels: Int,
  width: Int,
  height: Int
};

let printHeader = (title) => {
  print("");
  print(title);
};

let pass = (label) => {
  print(stringConcat("PASS: ", label))
};

let fail = (label, message) => {
  print(stringConcat("FAIL: ", stringConcat(label, message)))
};

let expectLetValueKind = (label, source, expectedKind: ExprKind) => {
  match (parseProgram(source)) {
    IOk(program) => {
      match (program.declarations) {
        Link(decl, _) => {
          let actualKind = exprKind(decl.value);
          match (actualKind, expectedKind) {
            (Identifier, Identifier) => { pass(label) },
            (Number, Number) => { pass(label) },
            (String, String) => { pass(label) },
            (Call, Call) => { pass(label) },
            _ => {
              fail(label, " unexpected value kind")
            }
          }
        },
        Empty => {
          fail(label, " program had no declarations")
        }
      }
    },
    IErr(err) => {
      fail(label, stringConcat(" parse error: ", err.message))
    }
  }
};

let expectParseError = (label, source) => {
  match (parseProgram(source)) {
    IOk(_) => {
      fail(label, " expected parse error but got success")
    },
    IErr(err) => {
      let pos = intToString(err.span.start);
      let msg = stringConcat("error at ", pos);
      pass(stringConcat(label, stringConcat(" (", msg)))
    }
  }
};

let main = => {
  printHeader("Parser tests");

  expectLetValueKind(
    "identifier literal",
    "let name = foo;\n",
    Identifier
  );

  expectLetValueKind(
    "number literal",
    "let count = 123;",
    Number
  );

  expectLetValueKind(
    "string literal",
    "let greeting = \"hi\";",
    String
  );

  expectLetValueKind(
    "call expression",
    "let answer = compute(1, helper());",
    Call
  );

  expectParseError(
    "missing semicolon",
    "let bad = 1"
  );
};
